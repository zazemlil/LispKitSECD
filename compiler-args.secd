(
    (LETREC COMPILE

        (COMPILE (LAMBDA (E) (
                COMP E (QUOTE NIL) (QUOTE (STOP))
            )
        ))
        (COMP (LAMBDA (E N C)
                (COND (ATOM E)
                    (CONS (QUOTE LD) (CONS (LOCATION E N) C))
                (COND (EQUAL (CAR E) (QUOTE QUOTE))
                    (CONS (QUOTE LDC) (CONS (CAR (CDR E)) C))
                (COND (EQUAL (CAR E) (QUOTE ADD))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE ADD) C)))
                (COND (EQUAL (CAR E) (QUOTE SUB))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE SUB) C)))
                (COND (EQUAL (CAR E) (QUOTE MUL))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE MUL) C)))
                (COND (EQUAL (CAR E) (QUOTE DIVE))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE DIVE) C)))
                (COND (EQUAL (CAR E) (QUOTE REM))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE REM) C)))
                (COND (EQUAL (CAR E) (QUOTE LE))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE LE) C)))
                (COND (EQUAL (CAR E) (QUOTE EQUAL))
                    (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS (QUOTE EQUAL) C)))
                (COND (EQUAL (CAR E) (QUOTE CAR))
                    (COMP (CAR (CDR E)) N (CONS (QUOTE CAR) C))
                (COND (EQUAL (CAR E) (QUOTE CDR))
                    (COMP (CAR (CDR E)) N (CONS (QUOTE CDR) C))
                (COND (EQUAL (CAR E) (QUOTE ATOM))
                    (COMP (CAR (CDR E)) N (CONS (QUOTE ATOM) C))
                (COND (EQUAL (CAR E) (QUOTE CONS))
                    (COMP (CAR (CDR (CDR E))) N (COMP (CAR (CDR E)) N (CONS (QUOTE CONS) C)))
                (COND (EQUAL (CAR E) (QUOTE COND))
                    (LET (COMP (CAR (CDR E)) N (CONS (QUOTE SEL) (CONS THENPT (CONS ELSEPT C))))
                    (THENPT (COMP (CAR (CDR (CDR E))) N (QUOTE (JOIN))))
                    (ELSEPT (COMP (CAR (CDR (CDR (CDR E)))) N (QUOTE (JOIN)))))
                (COND (EQUAL (CAR E) (QUOTE LAMBDA))
                    (LET (CONS (QUOTE LDF) (CONS BODY C))
                        (BODY (COMP (CAR (CDR (CDR E))) (CONS (CAR (CDR E)) N) (QUOTE (RTN)))))
                (COND (EQUAL (CAR E) (QUOTE LET))
                    (LET 
                        (LET 
                            (COMPLIS ARGS N (CONS (QUOTE LDF) (CONS BODY (CONS (QUOTE AP) C))))
                            (BODY (COMP (CAR (CDR E)) M (QUOTE (RTN))))
                        )
                        (M (CONS (VARS (CDR (CDR E))) N))
                        (ARGS (EXPRS (CDR (CDR E)))))
                (COND (EQUAL (CAR E) (QUOTE LETREC))
                    (LET  
                        (LET  
                            (CONS (QUOTE DUM) (COMPLIS ARGS M (CONS (QUOTE LDF) (CONS BODY (CONS (QUOTE RAP) C)))))
                            (BODY (COMP (CAR (CDR E)) M (QUOTE (RTN))))
                        )
                        (M (CONS (VARS (CDR (CDR E))) N))
                        (ARGS (EXPRS (CDR (CDR E)))))
                (COMPLIS (CDR E) N (COMP (CAR E) N (CONS (QUOTE AP) C)))
            )))))))))))))))))
        ))
        (COMPLIS (LAMBDA (E N C)
            (COND (EQUAL E (QUOTE NIL)) 
                (CONS (QUOTE LDC) (CONS (QUOTE NIL) C))
                (COMPLIS (CDR E) N (COMP (CAR E) N (CONS (QUOTE CONS) C)))
            ))
        )
        (LOCATION (LAMBDA (E1 N1)
            (LETREC
                (COND (MEMBER E1 (CAR N1)) 
                    (CONS (QUOTE 0) (CONS (POSN E1 (CAR N1)) NIL)) 
                    (INCAR (LOCATION E1 (CDR N1)))
                )
                
                (MEMBER (LAMBDA (E N)
                    (COND (EQUAL N (QUOTE NIL)) (QUOTE FALSE)
                    (COND (EQUAL E (CAR N)) (QUOTE TRUE) (MEMBER E (CDR N))))))
                (POSN (LAMBDA (E N)
                    (COND (EQUAL E (CAR N)) (QUOTE 0) (ADD (QUOTE 1) (POSN E (CDR N))))))
                (INCAR (LAMBDA (L) 
                    (CONS (ADD (QUOTE 1) (CAR L)) (CDR L))))
            ))
        )
        (VARS (LAMBDA (D)
            (COND (EQUAL D (QUOTE NIL)) (QUOTE NIL)
                (CONS (CAR (CAR D)) (VARS (CDR D)))))
        )
        (EXPRS (LAMBDA (D)
            (COND (EQUAL D (QUOTE NIL)) (QUOTE NIL)
                (CONS (CAR (CDR (CAR D))) (EXPRS (CDR D)))))
        )
    )
)